import groovy.swing.SwingBuilder
import groovy.xml.Namespace

/*
 * Created by Angel Leon (@gubatron), Alden Torres (aldenml)
 * Copyright (c) 2011-2025, FrostWire(R). All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.chaquo.python'
apply plugin: 'org.jetbrains.kotlin.android'

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://chaquo.com/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'org.codehaus.groovy:groovy-all:3.0.9'

        classpath 'net.sf.proguard:proguard-gradle:6.2.2'

        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'

        classpath 'com.chaquo.python:gradle:16.0.0'

        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.20'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

def manifestVersionCode() {
    def manifestFile = new File(project.projectDir.absolutePath + '/AndroidManifest.xml')
    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new groovy.xml.XmlParser().parse(manifestFile)
    //System.out.println("manifestVersionCode: xml.attributes()[ns.versionCode].toString()=" + xml.attributes()[ns.versionCode].toString())
    return Integer.parseInt(xml.attributes()[ns.versionCode].toString())
}

def manifestVersionName() {
    def manifestFile = file(project.projectDir.absolutePath + '/AndroidManifest.xml')
    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new groovy.xml.XmlParser().parse(manifestFile)
    //System.out.println("manifestVersionName: " + xml.attributes()[ns.versionName].toString())
    return xml.attributes()[ns.versionName].toString()
}

def changeApkOutput(variant) {
    def flavor = 'plus'
    def suffix = project.ext.versionName + '-b' + project.ext.versionCode + '-' + flavor
    def name = "frostwire-android-" + variant.buildType.name + '-' + suffix

    variant.outputs.all { output ->
        outputFileName = name + '.apk'
    }
    variant.getAssembleProvider().configure() {
        it.doLast {
            assemble ->
                copy {
                    from variant.outputs*.outputFile
                    into "${project.getLayout().getBuildDirectory().getAsFile().get()}"
                }
        }
    }
}

project.ext.versionCode = manifestVersionCode()
project.ext.versionName = manifestVersionName()

boolean isAssembleRelease = gradle.startParameter.taskNames.contains("assembleRelease") || gradle.startParameter.taskNames.contains("assemblePlus1Release")

android {
    namespace "com.frostwire.android"

    compileSdk 35  // Android 14 UpsideDownCake

    defaultConfig {
        applicationId "com.frostwire.android"
        versionName project.ext.versionName
        //minSdkVersion 24 // Android 7.0 Nougat, they added ifaddr (as an option to netlink)
        minSdkVersion 26 // Android 8.0 Oreo
        //minSdkVersion 28 // Android 9.0 Pie
        targetSdkVersion 34 // Android 14 (more involved upgrade, crashes on targetsdkversion 34)
        multiDexEnabled true

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        python {
            chaquopy {
                defaultConfig {
                    version "3.11"
                }
            }
            pip {
                install "yt_dlp==2024.11.18"
                // helps a lot with debugging
                //options "--verbose"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res', 'apollo/res']
            aidl.srcDirs = ['src', 'apollo/src']
            assets.srcDirs = ['assets']
            java {
                srcDir '../common/src/main/java'
                srcDir 'apollo/src'
                srcDir 'src'
            }
            jniLibs.srcDirs = ['libs']
        }
    }


    signingConfigs {
        release {
            if (isAssembleRelease) {
                storeFile file("../../frostwire-tools/certs/android.keystore/frostwire.keystore")
                def sigRequester = new SignatureRequester()
                sigRequester.ask()
                storePassword sigRequester.keyPassword()
                keyAlias sigRequester.keyAlias()
                keyPassword sigRequester.keyPassword()
            } else {
                storePassword ''
                keyAlias ''
                keyPassword ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
            multiDexKeepFile file('multidex-config.txt')
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            multiDexKeepFile file('multidex-config.txt')
        }
    }

    flavorDimensions "classicApp"

    productFlavors {
        /*
        basic {
            // OLD BASIC CODE VERSION SUFFIX 9060000
            versionCode 9080000 + project.ext.versionCode
            resValue "string", "application_label", "FrostWire"
        }
        */
        plus1 {
            // OLD PLUS CODE VERSION SUFFIX 9070000
            dimension "classicApp"
            versionCode 9090000 + project.ext.versionCode
            resValue "string", "application_label", "FrostWire Plus"
        }
    }
    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        changeApkOutput(variant)
    }

    configurations {
        configureEach {
            exclude group: "com.google.android.gms.common", module: "play-services-measurement"
            exclude group: 'com.google.android.gms.common', module: 'play-services-base'
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.2.0'
    implementation 'com.google.android.ump:user-messaging-platform:3.1.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.re2j:re2j:1.7'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.12'
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.20'

    // Firebase Crashlytics
    implementation platform('com.google.firebase:firebase-bom:33.7.0')
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'androidx.media2:media2-session:1.3.0'
    implementation 'androidx.media2:media2-widget:1.3.0'
    implementation 'androidx.media2:media2-player:1.3.0'

    // AppLovin Max
    implementation 'com.applovin:applovin-sdk:13.0.1'

    // AppLovin + Unity
    implementation 'com.applovin.mediation:unityads-adapter:4.12.4.0'

    def jlibtorrent_version = '1.2.19.0'
    implementation files('libs/jlibtorrent-' + jlibtorrent_version + '.jar')

    // att free phones use this and crashes
    implementation files('libs/jlibtorrent-android-arm-' + jlibtorrent_version + '.jar')

    // oneplus hd1905 uses this
    implementation files('libs/jlibtorrent-android-arm64-' + jlibtorrent_version + '.jar')

    // used by android emulator on macos
    implementation files('libs/jlibtorrent-android-x86-' + jlibtorrent_version + '.jar')

    implementation files('libs/jlibtorrent-android-x86_64-' + jlibtorrent_version + '.jar')
}

afterEvaluate {
    tasks.named('uploadCrashlyticsMappingFilePlus1Release').configure {
        dependsOn ':processPlus1ReleaseGoogleServices'
    }
}

class SignatureRequester {
    String myKeyAlias = ""
    String myKeyPassword = ""

    void ask() {
        System.setProperty('java.awt.headless', 'false')
        boolean useSwingDialog = true  // Force use of Swing dialog

        System.out.println("System.console()=" + System.console())

        if (useSwingDialog || System.console() == null) {
            // Force the Swing dialog to be used by always setting useSwingDialog to true
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter keystore data',
                        alwaysOnTop: true,
                        resizable: true,
                        locationRelativeTo: null,
                        show: true,
                        size: [215, 147]
                ) {
                    vbox {
                        label(text: "Key Alias:")
                        inputAlias = textField()
                        label(text: "Key Password:")
                        inputPassword = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            this.myKeyAlias = inputAlias.text
                            this.myKeyPassword = new String(inputPassword.password)
                            dispose()
                        })
                    }
                }
            }
        } else {
            this.myKeyAlias = new String(System.console().readPassword("\nKey Alias: "))
            this.myKeyPassword = new String(System.console().readPassword("\nKey Password: "))
        }
    }

    String keyAlias() {
        this.myKeyAlias
    }

    String keyPassword() {
        this.myKeyPassword
    }
}