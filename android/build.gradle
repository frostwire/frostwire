/*
 * Created by Angel Leon (@gubatron), Alden Torres (aldenml)
 * Copyright (c) 2011-2018, FrostWire(R). All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import groovy.swing.SwingBuilder
import groovy.xml.Namespace

apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0' // 'com.android.tools.build:gradle:3.4.0'
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
    maven { url 'https://jitpack.io' } // for Vungle
    flatDir { dirs 'libs' }
}

def manifestVersionCode() {
    def manifestFile = file(project.projectDir.absolutePath + '/AndroidManifest.xml')

    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)

    return Integer.parseInt(xml.attributes()[ns.versionCode].toString())
}

def manifestVersionName() {
    def manifestFile = file(project.projectDir.absolutePath + '/AndroidManifest.xml')

    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)

    return xml.attributes()[ns.versionName].toString()
}

def changeApkOutput(variant) {
    def flavor = 'unknown'
    if (variant.productFlavors[0].name == "basic") {
        flavor = 'basic'
    } else if (variant.productFlavors[0].name == "plus1") {
        flavor = 'plus'
    }
    def suffix = project.ext.versionName + '-b' + project.ext.versionCode + '-' + flavor
    def name = "frostwire-android-" + variant.buildType.name + '-' + suffix

    variant.outputs.all { output ->
        outputFileName = name + '.apk'
    }
    variant.assemble.doLast { assemble ->
        copy {
            from variant.outputs*.outputFile
            into "${project.buildDir}"
        }
    }
}

project.ext.versionCode = manifestVersionCode()
project.ext.versionName = manifestVersionName()

boolean isAssembleRelease = gradle.startParameter.taskNames.contains("assembleRelease")

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        versionName versionName
        minSdkVersion 19
        targetSdkVersion 29
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res', 'apollo/res']
            aidl.srcDirs = ['src', 'apollo/src']
            assets.srcDirs = ['assets']
            java {
                srcDir '../common/src/main/java'
                srcDir 'apollo/src'
                srcDir 'src'
            }
        }

        plus1.res.srcDirs = ['res-plus1']
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2g"
        jumboMode = true
    }

    signingConfigs {
        release {
            if (isAssembleRelease) {
                storeFile file("../../frostwire-tools/certs/android.keystore/frostwire.keystore")
                def sigRequester = new SignatureRequester()
                sigRequester.ask()
                storePassword sigRequester.keyPassword()
                keyAlias sigRequester.keyAlias()
                keyPassword sigRequester.keyPassword()
            } else {
                storePassword ''
                keyAlias ''
                keyPassword ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
            multiDexKeepFile file('multidex-config.txt')
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            multiDexKeepFile file('multidex-config.txt')
        }
    }

    flavorDimensions "classicApp"

    productFlavors {
        basic {
            applicationId 'com.frostwire.android'
            versionCode 9060000 + project.ext.versionCode
        }
        plus1 {
            applicationId 'com.frostwire.android'
            versionCode 9070000 + project.ext.versionCode
        }
    }

    applicationVariants.all { variant ->
        changeApkOutput(variant)
    }
}

dependencies {
// AndroidX
    //https://developer.android.com/jetpack/androidx/versions
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.cursoradapter:cursoradapter:1.0.0'
    implementation 'androidx.documentfile:documentfile:1.0.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.viewpager:viewpager:1.0.0' //viewpager2
    implementation 'com.google.android.material:material:1.1.0-alpha09'


// Other
    //https://developer.android.com/google/play/billing/billing_library_releases_notes
    implementation 'com.android.billingclient:billing:1.2'

    //https://github.com/google/re2j/releases
    implementation 'com.google.re2j:re2j:1.2'

    //https://github.com/google/gson/blob/master/CHANGELOG.md
    implementation 'com.google.code.gson:gson:2.8.5'

    //https://github.com/google/flexbox-layout/releases
    implementation 'com.google.android:flexbox:1.0.0'

    //https://github.com/square/picasso/blob/master/CHANGELOG.md
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'

    //https://square.github.io/okhttp/changelog_3x/
    implementation 'com.squareup.okhttp3:okhttp:3.12.2'
    // 3.13.x+ (3.14.0) is only compatible after android 5 (api level 21), android 4.4 (api 19)
    // 3.12.2 last compatible one for android 4.4 (3.14.2 works fine on Android 9)


// Ads
    //https://developers.google.com/admob/android/rel-notes
    implementation 'com.google.android.gms:play-services-ads:18.1.1'

    //https://mvnrepository.com/artifact/com.applovin/applovin-sdk
    implementation 'com.applovin:applovin-sdk:9.8.4' //previous 9.7.2

    //https://github.com/mopub/mopub-android-sdk/blob/master/CHANGELOG.md
    implementation('com.mopub:mopub-sdk-banner:5.8.0@aar') { transitive = true }
    implementation('com.mopub:mopub-sdk-interstitial:5.8.0@aar') { transitive = true }

    //https://github.com/Vungle/Android-SDK/blob/master/CHANGELOG.md
    implementation 'com.github.vungle:vungle-android-sdk:6.3.24'

    //https://github.com/mopub/mopub-android-mediation/blob/master/Vungle/CHANGELOG.md
    implementation 'com.mopub.mediation:vungle:6.3.24.0'


// JLibTorrent
    implementation files('libs/jlibtorrent-1.2.2.0.jar')
    implementation files('libs/jlibtorrent-android-arm-1.2.2.0.jar')
    implementation files('libs/jlibtorrent-android-x86-1.2.2.0.jar')
    implementation files('libs/jlibtorrent-android-arm64-1.2.2.0.jar')
    implementation files('libs/jlibtorrent-android-x86_64-1.2.2.0.jar')
}

class DeployRelease extends DefaultTask {

    String flavor = 'unknown'

    def version = project.ext.versionName
    def build = project.ext.versionCode

    static def runCmd(cmd) {
        println(cmd)

        def p = Runtime.getRuntime().exec(cmd)
        def stdOut = new BufferedReader(new InputStreamReader(p.getInputStream()))

        def s
        while ((s = stdOut.readLine()) != null) {
            println(s)
        }
    }

    @TaskAction
    def run() {

        println "Building and installing..."
        println "VERSION: $version"
        println "BUILD: $build"
        println "FLAVOR: $flavor"

        def suffix = version + '-b' + build + '-' + flavor
        def filename = "$project.buildDir/frostwire-android-release-" + suffix + '.apk'

        runCmd("adb uninstall com.frostwire.android")
        runCmd("./gradlew clean assembleRelease")
        runCmd("adb install $filename")
    }
}

class SignatureRequester {
    String myKeyAlias = ""
    String myKeyPassword = ""

    void ask() {
        System.setProperty('java.awt.headless', 'false')
        if (System.console() == null) { // this is always the case on macOS at least
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter keystore data',
                        alwaysOnTop: true,
                        resizable: true,
                        locationRelativeTo: null,
                        show: true,
                        size: [215, 147]
                ) {
                    vbox {
                        label(text: "Key Alias:")
                        inputAlias = textField()
                        label(text: "Key Password:")
                        inputPassword = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            this.myKeyAlias = inputAlias.text
                            this.myKeyPassword = new String(inputPassword.password)
                            dispose()
                        })
                    }
                }
            }
        } else {
            this.myKeyAlias = System.console().readPassword("\nKey Alias: ")
            this.myKeyAlias = new String(myKeyAlias)
            this.myKeyPassword = System.console().readPassword("\nKey Password: ")
            this.myKeyPassword = new String(myKeyPassword)
        }
    }

    String keyAlias() {
        this.myKeyAlias
    }

    String keyPassword() {
        this.myKeyPassword
    }
}

task deployBasic(type: DeployRelease) {
    flavor = 'basic'
}

task deployPlus(type: DeployRelease) {
    flavor = 'plus'
}
