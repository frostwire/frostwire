/*
 * Created by Angel Leon (@gubatron), Alden Torres (aldenml)
 * Copyright (c) 2011-2020, FrostWire(R). All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import groovy.swing.SwingBuilder
import groovy.xml.Namespace

apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'

        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

    flatDir { dirs 'libs' }
}

def manifestVersionCode() {
    def manifestFile = file(project.projectDir.absolutePath + '/AndroidManifest.xml')

    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)

    return Integer.parseInt(xml.attributes()[ns.versionCode].toString())
}

def manifestVersionName() {
    def manifestFile = file(project.projectDir.absolutePath + '/AndroidManifest.xml')

    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)

    return xml.attributes()[ns.versionName].toString()
}

def changeApkOutput(variant) {
    def flavor = 'unknown'
    if (variant.productFlavors[0].name == "basic") {
        flavor = 'basic'
    } else if (variant.productFlavors[0].name == "plus1") {
        flavor = 'plus'
    }
    def suffix = project.ext.versionName + '-b' + project.ext.versionCode + '-' + flavor
    def name = "frostwire-android-" + variant.buildType.name + '-' + suffix

    variant.outputs.all { output ->
        outputFileName = name + '.apk'
    }
    variant.getAssembleProvider().configure() {
        it.doLast {
            assemble ->
                copy {
                    from variant.outputs*.outputFile
                    into "${project.buildDir}"
                }
        }
    }

}

project.ext.versionCode = manifestVersionCode()
project.ext.versionName = manifestVersionName()

boolean isAssembleRelease = gradle.startParameter.taskNames.contains("assembleRelease") || gradle.startParameter.taskNames.contains("assemblePlus1Release")

android {

    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId 'com.frostwire.android'
        versionName project.ext.versionName
        minSdkVersion 21 // Lollipop 5.0
        targetSdkVersion 30 // Android 11
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res', 'apollo/res']
            aidl.srcDirs = ['src', 'apollo/src']
            assets.srcDirs = ['assets']
            java {
                srcDir '../common/src/main/java'
                srcDir 'apollo/src'
                srcDir 'src'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2g"
        jumboMode = true
    }

    signingConfigs {
        release {
            if (isAssembleRelease) {
                storeFile file("../../frostwire-tools/certs/android.keystore/frostwire.keystore")
                def sigRequester = new SignatureRequester()
                sigRequester.ask()
                storePassword sigRequester.keyPassword()
                keyAlias sigRequester.keyAlias()
                keyPassword sigRequester.keyPassword()
            } else {
                storePassword ''
                keyAlias ''
                keyPassword ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
            multiDexKeepFile file('multidex-config.txt')
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            multiDexKeepFile file('multidex-config.txt')
        }
    }

    flavorDimensions "classicApp"

    productFlavors {
        basic {
            versionCode 9060000 + project.ext.versionCode
            resValue "string", "application_label", "FrostWire"
        }
        plus1 {
            versionCode 9070000 + project.ext.versionCode
            resValue "string", "application_label", "FrostWire Plus"
        }
    }

    applicationVariants.all { variant ->
        changeApkOutput(variant)
    }
}

dependencies {
    implementation 'com.google.re2j:re2j:1.4'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.android.billingclient:billing:3.0.3'
    implementation 'com.google.android.gms:play-services-ads:20.0.0'
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'
    implementation 'com.google.android:flexbox:1.1.1'

    // Firebase Crashlytics
    implementation platform('com.google.firebase:firebase-bom:26.7.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    // MoPub SDK requirements - https://github.com/mopub/mopub-android-sdk#requirements
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.media2:media2-session:1.1.3'
    implementation 'androidx.media2:media2-widget:1.1.3'
    implementation 'androidx.media2:media2-player:1.1.3'
    implementation 'com.mopub.volley:mopub-volley:2.1.0'

    // MoPub SDK
    implementation('com.mopub:mopub-sdk-banner:5.16.3@aar') { transitive = true }
    implementation('com.mopub:mopub-sdk-fullscreen:5.16.3@aar') { transitive = true }

    // MoPub Mediation Partners
    implementation 'com.applovin:applovin-sdk:10.1.2'
    implementation 'com.mopub.mediation:applovin:10.1.1.0'
    implementation 'com.unity3d.ads:unity-ads:3.6.2'
    implementation 'com.mopub.mediation:unityads:3.6.0.0'

    def jlibtorrent_version='1.2.13.0'
    implementation files('libs/jlibtorrent-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-arm-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-x86-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-arm64-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-x86_64-' + jlibtorrent_version + '.jar')
}

class DeployRelease extends DefaultTask {

    String flavor = 'unknown'

    def version = project.ext.versionName
    def build = project.ext.versionCode

    static def runCmd(cmd) {
        println(cmd)

        def p = Runtime.getRuntime().exec(cmd)
        def stdOut = new BufferedReader(new InputStreamReader(p.getInputStream()))

        def s
        while ((s = stdOut.readLine()) != null) {
            println(s)
        }
    }

    @TaskAction
    def run() {
        println "Building and installing..."
        println "VERSION: $version"
        println "BUILD: $build"
        println "FLAVOR: $flavor"

        def suffix = version + '-b' + build + '-' + flavor
        def filename = "$project.buildDir/frostwire-android-release-" + suffix + '.apk'

        runCmd("adb uninstall com.frostwire.android")
        runCmd("./gradlew clean assembleRelease")
        runCmd("adb install $filename")
    }
}

class SignatureRequester {
    String myKeyAlias = ""
    String myKeyPassword = ""

    void ask() {
        System.setProperty('java.awt.headless', 'false')
        if (System.console() == null) { // this is always the case on macOS at least
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter keystore data',
                        alwaysOnTop: true,
                        resizable: true,
                        locationRelativeTo: null,
                        show: true,
                        size: [215, 147]
                ) {
                    vbox {
                        label(text: "Key Alias:")
                        inputAlias = textField()
                        label(text: "Key Password:")
                        inputPassword = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            this.myKeyAlias = inputAlias.text
                            this.myKeyPassword = new String(inputPassword.password)
                            dispose()
                        })
                    }
                }
            }
        } else {
            this.myKeyAlias = System.console().readPassword("\nKey Alias: ")
            this.myKeyAlias = new String(myKeyAlias)
            this.myKeyPassword = System.console().readPassword("\nKey Password: ")
            this.myKeyPassword = new String(myKeyPassword)
        }
    }

    String keyAlias() {
        this.myKeyAlias
    }

    String keyPassword() {
        this.myKeyPassword
    }
}

task deployBasic(type: DeployRelease) {
    flavor = 'basic'
}

task deployPlus(type: DeployRelease) {
    flavor = 'plus'
}
